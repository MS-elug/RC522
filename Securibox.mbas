#DEFINE DEBUG_TO_UART_MODE

program Securibox

'*******************************************************************************
'
'   PIC 18F45K22
'   Quartz 16 MHz + PLL X4  => 64.0000 MHz
'
'  C.0 = Slave Select pin to connect to SS input of RC522
'  C.1 = Slave Reset pin to connect to RST input of RC522
'  Optional:
'  C.6 = UART TX output
'  C.7 = UART RX input
'*******************************************************************************

include RC522

sub procedure initMain()
   TRISA = %00000000                   ' A en sortie
   TRISB = %00000000                   ' B en sortie
   TRISC = %00000000                ' C en sortie

   PORTA = %00000000                   ' initialize Port A
   PORTB = %00000000                   ' initialize Port B
   PORTC = %00000000                   ' initialize Port C

   TRISE = 0x00
   PORTE = 0x00                                     ' initialize port E

   ' -----------------------------------------------------------
   ' Use 16Mhz externql clock with PLL4x to get 64Mhz clock
   OSCCON.SCS1 = 0                    ' System clock set to primary clock
   OSCCON.SCS0 = 0

   ' -----------------------------------------------------------
   ' Initialize A/D module
   ANSELA = 0x00
   ANSELB = 0x00                      ' aucune entr?e analogique en B
   ANSELC = 0x00                      ' aucune entr?e analogique en C
   
   CM1CON0.7=0                                   ' comparateurs arr?t?s
   CM2CON0.7=0                                   ' comparateurs arr?t?s

  'RC522_SS_PIN = 1
  'RC522_RST_PIN = 0
  
  #IFDEF DEBUG_TO_UART_MODE THEN 'Initialize UART
      UART1_Init(9600)
  #ENDIF
  
  ' Init RC522 chip
  ' /!\ DO NOT FORGET TO UPDATE POINTER RC522_SS_PIN,RC522_SS_PIN_TRIS,RC522_RST_PIN and RC522_RST_PIN_TRIS in RC522.mbas
  ' MFRC522 accept upto 10MHz SPI clock
  SPI1_Init_Advanced(_SPI_MASTER_OSC_DIV16, _SPI_DATA_SAMPLE_MIDDLE, _SPI_CLK_IDLE_LOW, _SPI_LOW_2_HIGH)
  PCD_Init()
  
end sub

 
main:
  dim i as word
  #IFDEF DEBUG_TO_UART_MODE THEN
  dim txt as string[2]
  #ENDIF

  initMain()
  while(true)
    delay_ms(200)

    if(not PICC_IsNewCardPresent()) then
      continue
    end if
    
    #IFDEF DEBUG_TO_UART_MODE THEN
      UART1_Write_Text("Card found - Reading uid")
      UART1_Write(0x0A)
    #ENDIF

    if(not PICC_ReadCardSerial()) then
      #IFDEF DEBUG_TO_UART_MODE THEN
         UART1_Write_Text("Cannot read the card")
         UART1_Write(0x0A)
      #ENDIF
      continue
    end if

    if(uid.size <> 4) then
      #IFDEF DEBUG_TO_UART_MODE THEN
         UART1_Write_Text("Card not supported")
         UART1_Write(0x0A)
      #ENDIF
      continue
    
    end if
    #IFDEF DEBUG_TO_UART_MODE THEN
      UART1_Write_Text("UID card is ")
      for i=0 to uid.size - 1
        ByteToHex(uid.uidByte[i], txt)
        UART1_Write_Text(txt)
        UART1_Write_Text(" ")
      next i
      UART1_Write(0x0A)
    #ENDIF
  wend
  
end.